subprojects {
	apply plugin: 'groovy'
	apply plugin: 'eclipse'

	repositories {
	    mavenCentral()
	}

	dependencies {
	    compile 'org.codehaus.groovy:groovy-all:2.4.6'
	    compile 'org.springframework:spring-context:4.3.2.RELEASE'
	    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
	    testCompile 'com.blogspot.toomuchcoding:spock-subjects-collaborators-extension:1.2.1'
	    testCompile 'junit:junit:4.12'
	}
}

['text', 'regex', 'index'].each { module ->
	project("search:$module") {
		dependencies {
			compile project(":api")
		}
	}
}

allprojects {
  apply plugin: 'jacoco'
}

task codeCoverageReport(type: JacocoReport) {

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
       sourceSets it.sourceSets.main
    }

    reports {
      html.enabled true
      html.destination "${buildDir}/reports/jacoco"
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}